apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.zinuzoid.zlog'
version = '0.1.6'

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar, dependsOn: generateJavadocs) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.file('bintray.properties').newDataInputStream())
bintray {
    user = bintrayProperties.getProperty('BinTray.user')
    key = bintrayProperties.getProperty('BinTray.key')

    configurations = ['archives']
    dryRun = false
    pkg {
        repo = 'Android'
        name = 'com.zinuzoid.zlog'

        websiteUrl = 'https://github.com/zinuzoid/ZLog'
        issueTrackerUrl = 'https://github.com/zinuzoid/ZLog/issues'
        vcsUrl = 'https://github.com/zinuzoid/ZLog.git'
        licenses = ['GPL-3.0']
        labels = ['android', 'log']
    }
}



